#+ description: Connects placenames on maps collection UvA to Gemeentegeschiedenis polygons
#+     Note: nested query with counts and legend does not work because of server timeout (due to the memory intensive label matching)
#+     endpoint: https://api.lod.uba.uva.nl/datasets/UB-UVA/Catalogue/services/virtuoso/sparql

PREFIX geo: <http://www.opengis.net/ont/geosparql#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX schema: <http://schema.org/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX gg: <http://www.gemeentegeschiedenis.nl/gg-schema#>

SELECT (count((?label_lod)) as ?n) ?label_lod ?wkt ?wktTooltip ?mapEndpoint WHERE {
  {
  ?work a <http://schema.org/Map>.
  ?work dc:date ?date .
  ?work dct:spatial ?spatial .
  ?spatial skos:prefLabel ?label_lod .
}

  service <https://api.druid.datalegend.net/datasets/nlgis/gemeentegeschiedenis/services/gemeentegeschiedenis/sparql> {
  
    ?sub gg:name ?label .
    ?sub geo:hasGeometry [gg:year ?max ;
                                    geo:asWKT ?wkt ] .
      BIND(xsd:string(?label) as ?label2)
    }
    FILTER(?label_lod = ?label2)
    BIND("https://mapwarper.net/maps/wms/38227?request=GetCapabilities&service=WMS&version=1.1.1" as ?mapEndpoint)
    BIND(STRAFTER(STR(?sub), 'gemeentenaam/') as ?wktTooltip)
  }
    # to add: legend with colors (doesn't work server timeout)
 #limit 10
