#+ summary: Fetch location of archive plus its url of author. 
#+ endpoint: https://api.druid.datalegend.net/datasets/RubenS/kerouac-vde/services/kerouac-vde/sparql

PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX bib: <http://id.loc.gov/ontologies/bibframe/>
PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX geo: <http://www.opengis.net/ont/geosparql#>

SELECT DISTINCT * WHERE {
   
  ?worldcat_author a bib:Agent .
  ?worldcat_author   rdfs:label ?label .
  ?worldcat_author  owl:sameAs wd:Q160534

  FILTER(?label = "Kerouac, Jack,1922-1969.")
 
service <https://query.wikidata.org/bigdata/namespace/wdq/sparql> {
   
   wd:Q160534 wdt:P1477 ?birth_name ;
                wdt:P485 ?kerouac_archives .
    ?kerouac_archives rdfs:label ?archive_name .    
    ?kerouac_archives wdt:P625 ?wkt . 
    ?kerouac_archives wdt:P856 ?archive_website .
    FILTER(lang(?archive_name) = 'en')
   
     }

  BIND((?archive_website) as ?wktTooltip)
} LIMIT 100
